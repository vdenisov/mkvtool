// Plugin versions are pinned in gradle.properties (single source of truth). Declaring
// them here means build.gradle.kts applies the plugins without repeating versions.
pluginManagement {
    val kotlinVersion = providers.gradleProperty("kotlinVersion").get()
    val shadowVersion = providers.gradleProperty("shadowVersion").get()
    val graalvmNativeVersion = providers.gradleProperty("graalvmNativeVersion").get()
    plugins {
        kotlin("jvm") version kotlinVersion
        kotlin("kapt") version kotlinVersion
        // Ships with the Kotlin distribution, so it is versioned with the compiler.
        kotlin("plugin.serialization") version kotlinVersion
        id("com.gradleup.shadow") version shadowVersion
        id("org.graalvm.buildtools.native") version graalvmNativeVersion
    }
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }
}

dependencyResolutionManagement {
    repositories {
        mavenCentral()
    }
}

rootProject.name = "mkvtool"
